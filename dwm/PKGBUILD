pkgname=dwm-mseeber
_pkgname=dwm
pkgver=6.2
pkgrel=1
pkgdesc="A dynamic window manager for X"
url="http://dwm.suckless.org"
arch=('i686' 'x86_64')
license=('MIT')
options=(zipman)
depends=('libx11' 'libxinerama' 'libxft' 'fontconfig')
makedepends=('git')
install=dwm.install
provides=('dwm')
conflicts=('dwm')
epoch=1
source=(dwm.desktop \
        "https://dl.suckless.org/dwm/${_pkgname}-${pkgver}.tar.gz" \
        'config.h' \
        'https://dwm.suckless.org/patches/systray/dwm-systray-20190208-cb3f58a.diff' \
        'https://dwm.suckless.org/patches/autostart/dwm-autostart-20161205-bb3bd6f.diff')

md5sums=('939f403a71b6e85261d09fc3412269ee'
         'SKIP'
         'SKIP'
         'SKIP'
         'SKIP')


prepare() {
  cp -f config.h ${srcdir}/${_pkgname}-${pkgver}/config.h
  cd "${srcdir}/${_pkgname}-${pkgver}"
  patch -p1 -i ${srcdir}/dwm-systray-20190208-cb3f58a.diff
  patch -p1 -i ${srcdir}/dwm-autostart-20161205-bb3bd6f.diff
}

build() {
  cd ${_pkgname}-${pkgver}
  make X11INC=/usr/include/X11 X11LIB=/usr/lib/X11
}

package() {
  cd ${_pkgname}-${pkgver}
  make PREFIX=/usr DESTDIR="$pkgdir" install
  install -m644 -D LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  install -m644 -D README "$pkgdir/usr/share/doc/$pkgname/README"
  install -m644 -D ../dwm.desktop "$pkgdir/usr/share/xsessions/dwm.desktop"
}

# vim:set ts=2 sw=2 et:
