# Maintainer: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Scytrin dai Kinthra <scytrin@gmail.com>
# Modified: Markus Seeber

pkgname=st-git
_pkgname=st
epoch=1
pkgver=20160201.6d636be
pkgrel=1
pkgdesc='Simple virtual terminal emulator for X'
url='http://git.suckless.org/st/'
arch=('i686' 'x86_64' 'armv7h')
license=('MIT')
depends=('libxft')
makedepends=('ncurses' 'libxext' 'git')
source=('git://git.suckless.org/st' \
	'http://st.suckless.org/patches/st-git-20150917-no-bold-colors.diff' \
	'st-solarized-toggle-bg.diff')
sha1sums=('SKIP' \
	'a73b9bcdc059f448b4d6dfc8b4076f910bb1639a' \
	'115e91a12702f2f00512e15577f1e62377823e61')

provides=("${_pkgname}")
conflicts=("${_pkgname}")

pkgver() {
	cd "${srcdir}/${_pkgname}"
	git log -1 --format='%cd.%h' --date=short | tr -d -
}

prepare() {
	cd "${srcdir}/${_pkgname}"
	sed \
		-e 's/CPPFLAGS =/CPPFLAGS +=/g' \
		-e 's/CFLAGS =/CFLAGS +=/g' \
		-e 's/LDFLAGS =/LDFLAGS +=/g' \
		-i config.mk
	sed '/char font/s/".*"/"Inconsolata:pixelsize=28:antialias=true:autohint=true"/' -i config.def.h
	patch -p1 -i $srcdir/st-git-20150917-no-bold-colors.diff
	patch -p1 -i $srcdir/st-solarized-toggle-bg.diff
}

build() {
	cd "${srcdir}/${_pkgname}"
	make X11INC=/usr/include/X11 X11LIB=/usr/lib/X11
}

package() {
	cd "${srcdir}/${_pkgname}"
	export TERMINFO="${pkgdir}/usr/share/terminfo"
	install -d "$TERMINFO"
	make PREFIX=/usr DESTDIR="${pkgdir}" install
	install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
	install -Dm644 README "${pkgdir}/usr/share/doc/${pkgname}/README"
}
