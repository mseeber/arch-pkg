# Maintainer: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Scytrin dai Kinthra <scytrin@gmail.com>
# Modified: Markus Seeber

pkgname=st-git
_pkgname=st
epoch=1
pkgver=20161216.c63a87c
pkgrel=1
pkgdesc='Simple virtual terminal emulator for X'
url='http://git.suckless.org/st/'
arch=('i686' 'x86_64' 'armv7h')
license=('MIT')
depends=('libxft')
makedepends=('ncurses' 'libxext' 'git')
source=('git://git.suckless.org/st#commit=c63a87c' \
	'http://st.suckless.org/patches/st-no_bold_colors-20160727-308bfbf.diff' \
	'http://st.suckless.org/patches/st-delkey-20160727-308bfbf.diff'
	'st-solarized-toggle-bg.diff')
sha1sums=('SKIP' \
	'd089f953f091933fa867416648d497a87bde3461' \
	'1b3a6708a3644414fa26f07db8b2e1bb719f0381' \
	'SKIP')

provides=("${_pkgname}")
conflicts=("${_pkgname}")

pkgver() {
	cd "${srcdir}/${_pkgname}"
	git log -1 --format='%cd.%h' --date=short | tr -d -
}

prepare() {
	cd "${srcdir}/${_pkgname}"
	sed \
		-e 's/CPPFLAGS =/CPPFLAGS +=/g' \
		-e 's/CFLAGS =/CFLAGS +=/g' \
		-e 's/LDFLAGS =/LDFLAGS +=/g' \
		-i config.mk
	sed '/char font/s/".*"/"Hack:pixelsize=21:antialias=true:autohint=true"/' -i config.def.h
	sed 's/static int borderpx = 2/static int borderpx = 0/g' -i config.def.h
	patch -p1 -i ${srcdir}/st-no_bold_colors-20160727-308bfbf.diff
	#patch -p1 -i ${srcdir}/st-solarized-toggle-bg.diff
	patch -p1 -i ${srcdir}/st-delkey-20160727-308bfbf.diff
}

build() {
	cd "${srcdir}/${_pkgname}"
	make X11INC=/usr/include/X11 X11LIB=/usr/lib/X11
}

package() {
	cd "${srcdir}/${_pkgname}"
	export TERMINFO="${pkgdir}/usr/share/terminfo"
	install -d "$TERMINFO"
	make PREFIX=/usr DESTDIR="${pkgdir}" install
	install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
	install -Dm644 README "${pkgdir}/usr/share/doc/${pkgname}/README"
}
