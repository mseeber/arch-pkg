pkgname=dwm-git
_pkgname=dwm
pkgver=6.1.14.gbb3bd6f
pkgrel=1
pkgdesc="A dynamic window manager for X"
url="http://dwm.suckless.org"
arch=('i686' 'x86_64')
license=('MIT')
options=(zipman)
depends=('libx11' 'libxinerama' 'libxft' 'fontconfig')
makedepends=('git')
install=dwm.install
provides=('dwm')
conflicts=('dwm')
epoch=1
source=(dwm.desktop \
        "$_pkgname::git+http://git.suckless.org/dwm" \
        'config.h' \
        'dwm-st-copy-conflict.diff' \
        'http://dwm.suckless.org/patches/dwm-systray-20161218-bb3bd6f.diff')

md5sums=('939f403a71b6e85261d09fc3412269ee'
         'SKIP'
         'SKIP'
         'SKIP'
         'SKIP')

pkgver(){
  cd $_pkgname
  git describe --tags |sed 's/-/./g'
}

prepare() {
  cp -f ${srcdir}/config.h ${_pkgname}/config.h
  cd "${srcdir}/${_pkgname}"
  patch -p1 -i ${srcdir}/dwm-systray-20161218-bb3bd6f.diff
  patch -p1 -i ${srcdir}/dwm-st-copy-conflict.diff
}

build() {
  cd $_pkgname
  make X11INC=/usr/include/X11 X11LIB=/usr/lib/X11
}

package() {
  cd $_pkgname
  make PREFIX=/usr DESTDIR="$pkgdir" install
  install -m644 -D LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  install -m644 -D README "$pkgdir/usr/share/doc/$pkgname/README"
  install -m644 -D ../dwm.desktop "$pkgdir/usr/share/xsessions/dwm.desktop"
}

# vim:set ts=2 sw=2 et:
